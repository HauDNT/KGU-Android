plugins {
    id 'com.android.application'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')

if (localPropertiesFile.exists()) {
    localPropertiesFile.withInputStream { stream -> localProperties.load(stream)}
}

android {
    namespace 'com.application.application'
    compileSdk 34

    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        applicationId "com.application.application"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Local variables for project:
            // 1. Database name
            if (localProperties.containsKey('DATABASE_NAME')) {
                buildConfigField "String", "DATABASE_NAME", "\"${localProperties['DATABASE_NAME']}\""
            } else {
                throw new GradleException("DATABASE_NAME is not defined in local.properties")
            }

            // 2. Database version
            if (localProperties.containsKey('DATABASE_VERSION')) {
                buildConfigField "String", "DATABASE_VERSION", "\"${localProperties['DATABASE_VERSION']}\""
            } else {
                throw new GradleException("DATABASE_VERSION is not defined in local.properties")
            }
        }

        debug {
            buildConfigField "String", "DATABASE_NAME", "\"${localProperties['DATABASE_NAME']}\""
            buildConfigField "String", "DATABASE_VERSION", "\"${localProperties['DATABASE_VERSION']}\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

}